$TradeUI::pollMS     = 2000;
$TradeUI::timer      = 0;
$TradeUI::npcId      = 0;
$TradeUI::lastRxTime = 0;
$TradeUI::mode       = "buy";

$TradeUI::root   = "";
$TradeUI::stack  = "";
$TradeUI::text   = "";
$TradeUI::hdrAct = "";

// --- helpers ----------------------------------------------------------

function TradeUI__find(%root, %name)
{
  if (!isObject(%root)) return 0;
  if (%root.internalName $= %name || %root.getName() $= %name) return %root;
  for (%i=0; %i<%root.getCount(); %i++) {
    %f = TradeUI__find(%root.getObject(%i), %name);
    if (isObject(%f)) return %f;
  }
  return 0;
}

function TradeUI_Bind(%root)
{
  $TradeUI::root   = %root;
  $TradeUI::stack  = TradeUI__find(%root,"ItemsStack");
  $TradeUI::text   = TradeUI__find(%root,"ItemsText");
  $TradeUI::hdrAct = TradeUI__find(%root,"ActionHeader");
  TradeUI__updateHeader();
}

function TradeUI_ShowList(){ if (isObject($TradeUI::stack)) $TradeUI::stack.setVisible(true); if (isObject($TradeUI::text)) $TradeUI::text.setVisible(false); }
function TradeUI_ShowSoon(){
  if (isObject($TradeUI::stack)) $TradeUI::stack.setVisible(false);
  if (isObject($TradeUI::text)){$TradeUI::text.setVisible(true);$TradeUI::text.setText("<just:center><font:Arial Bold:18><color:FFCC66>Coming soon!");}
}

function TradeUI_RequestList(%npcId)
{
  if (%npcId $= "") %npcId = $TradeUI::npcId; $TradeUI::npcId=%npcId;
  if (!isObject(ServerConnection)) return;
  commandToServer('Trade_Request', %npcId);
  schedule(1000,0,"TradeUI__watchdog", getSimTime());
}

function TradeUI_RequestListSell(%npcId)
{
  if (%npcId $= "") %npcId = $TradeUI::npcId; $TradeUI::npcId=%npcId;
  if (!isObject(ServerConnection)) return;
  commandToServer('Trade_RequestSell', %npcId);
  schedule(1000,0,"TradeUI__watchdog", getSimTime());
}

function TradeUI__watchdog(%t0)
{
  if ($TradeUI::lastRxTime < %t0) {
    if ($TradeUI::mode $= "sell") TradeUI_RequestListSell($TradeUI::npcId);
    else TradeUI_RequestList($TradeUI::npcId);
  }
}

function TradeUI_RefreshStart()
{
  if ($TradeUI::timer !$= "0") return;
  if ($TradeUI::mode $= "sell") TradeUI_RequestListSell($TradeUI::npcId);
  else TradeUI_RequestList($TradeUI::npcId);
  $TradeUI::timer = schedule($TradeUI::pollMS, 0, "TradeUI_RefreshStart");
}

function TradeUI_RefreshStop()
{
  if ($TradeUI::timer $= "0") return;
  cancel($TradeUI::timer); $TradeUI::timer=0;
}

function TradeUI_SwitchMode(%mode)
{
  if (%mode !$= "buy" && %mode !$= "sell") %mode = "buy";
  $TradeUI::mode = %mode;
  TradeUI__updateHeader();
  TradeUI_RefreshStop();
  if ($TradeUI::mode $= "sell") TradeUI_RequestListSell($TradeUI::npcId);
  else TradeUI_RequestList($TradeUI::npcId);
}

function TradeUI__updateHeader()
{
  if (isObject($TradeUI::hdrAct))
    $TradeUI::hdrAct.setText($TradeUI::mode $= "sell" ? "Sell" : "Buy");
}

if (!isObject(TradeQtyEditProfile))
new GuiControlProfile(TradeQtyEditProfile : NewGuiTextEditProfile)
{
  opaque = true; fillColor="235 235 235 255"; fillColorHL="245 245 245 255";
  border="line"; borderColor="120 120 120 255";
  fontColor="30 30 30 255"; fontColorHL="0 0 0 255";
};

if (!isObject(TradePriceTextProfile))
new GuiControlProfile(TradePriceTextProfile : GuiTextProfile)
{
  fontType="Arial Bold"; fontSize=14; fontColor="32 32 32 255";
};

function TradeUI_UpdateRowTotal(%qtyEdit)
{
  if (!isObject(%qtyEdit)) return;
  %row = %qtyEdit.getParent(); if (!isObject(%row)) return;

  %qty = mFloor(trim(%qtyEdit.getText())); if (%qty <= 0) %qty = 1;
  %unit = (%row.unitPrice $= "" ? 0 : %row.unitPrice + 0);
  %qtyEdit.setText(%qty);

  %total = %unit * %qty;
  if (isObject(%row.priceCtrl)) %row.priceCtrl.setText(%total);
}

function TradeUI_AddRow(%id,%name,%icon,%q,%price,%pcs,%dur)
{
  if (!isObject($TradeUI::stack)) return;

  %row = new GuiControl() { profile="GuiDefaultProfile"; position="0 0"; extent="688 40"; };
  %row.unitPrice = (%price $= "" ? 0 : %price + 0);
  $TradeUI::stack.add(%row);

  // Ikona
  %row.add(new GuiBitmapCtrl(){ profile="GuiDefaultProfile"; position="6 4"; extent="32 32"; bitmap=%icon; canHit="0"; });

  // Nazwa
  %row.add(new GuiMLTextCtrl(){
    profile="GuiMLTextProfile"; position="48 8"; extent="230 24";
    allowColorChars="1"; text="<font:Arial Bold:16><color:202020>" @ %name;
  });

  // Price: jednolinijkowy
  %priceTxt = new GuiTextCtrl(){
    internalName="PriceTxt"; profile="TradePriceTextProfile";
    position="300 8"; extent="140 24"; justify="right"; text=%row.unitPrice;
  };
  %row.add(%priceTxt); %row.priceCtrl = %priceTxt;

  // Quality
  %row.add(new GuiMLTextCtrl(){
    profile="GuiMLTextProfile"; position="450 8"; extent="40 24";
    allowColorChars="1"; text="<font:Arial Bold:14><color:202020>Q" @ %q; justify="right";
  });

  // Qty – bez limitów, live-update
  %row.add(new GuiTextEditCtrl(){
    internalName="QtyEdit"; profile="TradeQtyEditProfile";
    position="510 6"; extent="60 24";
    text="1"; maxLength="12"; numbersOnly="1";
    validate="1"; command="TradeUI_UpdateRowTotal($ThisControl);";
    altCommand="TradeUI_UpdateRowTotal($ThisControl);";
    tooltip="Enter any quantity";
  });

  // Akcja – zależnie od zakładki
  %label = ($TradeUI::mode $= "sell" ? "Sell" : "Buy");
  %cmd   = ($TradeUI::mode $= "sell" ?
            "TradeUI_OnSell(" @ %id @ ", $ThisControl);" :
            "TradeUI_OnBuy("  @ %id @ ", $ThisControl);");

  %row.add(new GuiButtonCtrl(){
    profile="GuiButtonProfile"; position="586 6"; extent="90 28";
    text=%label; command=%cmd; tooltip=%label SPC %name;
  });
}

function TradeUI_ClearList()
{
  if (isObject($TradeUI::stack))
    while ($TradeUI::stack.getCount()>0) $TradeUI::stack.getObject(0).delete();
}

function TradeUI_Fill(%payload)
{
  $TradeUI::lastRxTime = getSimTime();
  $TradeUI::mode = "buy"; TradeUI__updateHeader();
  TradeUI_ShowList(); TradeUI_ClearList();

  %rows = strreplace(%payload,"|","\t");
  %n = getFieldCount(%rows);
  for (%i=0; %i<%n; %i++){
    %r=getField(%rows,%i); if (trim(%r) $= "") continue;
    %p=strreplace(%r,";","\t");
    TradeUI_AddRow(getField(%p,0),getField(%p,1),getField(%p,2),getField(%p,3),getField(%p,4),getField(%p,5),getField(%p,6));
  }
}

function TradeUI_FillSell(%payload)
{
  $TradeUI::lastRxTime = getSimTime();
  $TradeUI::mode = "sell"; TradeUI__updateHeader();
  TradeUI_ShowList(); TradeUI_ClearList();

  %rows = strreplace(%payload,"|","\t");
  %n = getFieldCount(%rows);
  for (%i=0; %i<%n; %i++){
    %r=getField(%rows,%i); if (trim(%r) $= "") continue;
    %p=strreplace(%r,";","\t");
    TradeUI_AddRow(getField(%p,0),getField(%p,1),getField(%p,2),getField(%p,3),getField(%p,4),getField(%p,5),getField(%p,6));
  }
}

function TradeUI_OnBuy(%id, %btn)
{
  %qty = 1;
  if (isObject(%btn)) {
    %row = %btn.getParent();
    %e = TradeUI__find(%row,"QtyEdit");
    if (isObject(%e)) { %n=mFloor(trim(%e.getText())); if (%n<=0)%n=1; %qty=%n; %e.setText(%qty); }
  }
  echo("[TradeUI] Buy id=" @ %id @ " qty=" @ %qty @ " npc=" @ $TradeUI::npcId);
  if (isObject(ServerConnection)) commandToServer('Trade_Buy', $TradeUI::npcId, %id, %qty);
}

function TradeUI_OnSell(%id, %btn)
{
  %qty = 1;
  if (isObject(%btn)) {
    %row = %btn.getParent();
    %e = TradeUI__find(%row,"QtyEdit");
    if (isObject(%e)) { %n=mFloor(trim(%e.getText())); if (%n<=0)%n=1; %qty=%n; %e.setText(%qty); }
  }
  echo("[TradeUI] Sell id=" @ %id @ " qty=" @ %qty @ " npc=" @ $TradeUI::npcId);
  if (isObject(ServerConnection)) commandToServer('Trade_Sell', $TradeUI::npcId, %id, %qty);
}

function clientCmdTrade_Open(%payload)
{
  $TradeUI::lastRxTime = getSimTime();
  if (!isObject(GuiTrade) || Canvas.getContent() != GuiTrade) Canvas.pushDialog(GuiTrade);
  TradeUI_Bind(GuiTrade);
  TradeUI_Fill(%payload);
}

function clientCmdTrade_OpenSell(%payload)
{
  $TradeUI::lastRxTime = getSimTime();
  if (!isObject(GuiTrade) || Canvas.getContent() != GuiTrade) Canvas.pushDialog(GuiTrade);
  TradeUI_Bind(GuiTrade);
  TradeUI_FillSell(%payload);
}

if (isObject(GuiTrade)) GuiTrade.delete();

new GuiControl(GuiTrade)
{
  profile="GuiOverlayProfile"; isContainer="1"; position="0 0"; extent="760 540";

  new GuiWindowCtrl()
  {
    profile="GuiWindowProfile";
    text="Trade";
    canClose="1"; canMove="1";
    canMinimize="0"; canMaximize="0";
    resizeWidth="0"; resizeHeight="0";
    closeCommand="Canvas.popDialog(GuiTrade);";

    position="10 10"; extent="740 520";

    new GuiBitmapCtrl() {
      position="12 44"; extent="716 464";
      bitmap="gui/images/Main_back_ornament.png"; canHit="0";
      horizSizing="width"; vertSizing="height";
    };

    new GuiControl() { position="24 56"; extent="692 24";
      new GuiButtonCtrl(){ profile="GuiButtonProfile"; text="Buy";  position="0 0";   extent="70 24";
        command="TradeUI_SwitchMode(\"buy\");"; };
      new GuiButtonCtrl(){ profile="GuiButtonProfile"; text="Sell"; position="76 0";  extent="70 24";
        command="TradeUI_SwitchMode(\"sell\");"; };
      new GuiButtonCtrl(){ profile="GuiButtonProfile"; text="Refresh"; position="152 0"; extent="80 24";
        command="if($TradeUI::mode $= \"sell\") TradeUI_RequestListSell($TradeUI::npcId); else TradeUI_RequestList($TradeUI::npcId);"; };
    };

    new GuiControl(){ position="24 92"; extent="692 22";
      new GuiTextCtrl(){ profile="GuiTextProfile"; text="Icon";  position="6 2";   extent="40 18"; };
      new GuiTextCtrl(){ profile="GuiTextProfile"; text="Name";  position="70 2";  extent="200 18"; };
      new GuiTextCtrl(){ profile="GuiTextProfile"; text="Price"; position="300 2"; extent="140 18"; justify="right"; };
      new GuiTextCtrl(){ profile="GuiTextProfile"; text="Q";     position="450 2"; extent="40 18";  justify="right"; };
      new GuiTextCtrl(){ profile="GuiTextProfile"; text="Qty";   position="510 2"; extent="50 18";  justify="right"; };
      new GuiTextCtrl(ActionHeader){ profile="GuiTextProfile"; text="Buy"; position="586 2"; extent="60 18";  justify="right"; };
    };

    // list
    new GuiScrollCtrl(){
      internalName="ItemsScroll"; position="24 116"; extent="668 380";
      hScrollBar="alwaysOff"; vScrollBar="dynamic"; lockHorizScroll="true";
      willFirstRespond="1"; constantThumbHeight="0";

      new GuiStackControl(ItemsStack){
        internalName="ItemsStack"; position="0 0"; extent="668 380";
        stackingType="Vertical"; padding="2"; dynamicSize="1"; changeChildSizeToFit="1";
      };

      new GuiMLTextCtrl(ItemsText){
        internalName="ItemsText"; position="0 0"; extent="668 380";
        visible="0"; allowColorChars="1"; lineSpacing="2";
      };
    };
  };
};

// onWake/onSleep
function GuiTrade::onWake(%this){ TradeUI_Bind(%this); TradeUI_ShowList(); TradeUI_RefreshStart(); }
function GuiTrade::onSleep(%this){ TradeUI_RefreshStop(); }
